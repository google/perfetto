# Copyright (C) 2019 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("perfetto.gni")
import("test.gni")

# To have a faster feedback loop, we want to be able to build and run only the
# relevant subset of perfetto unittests. We define the following lists of
# tests, each of them produces the separate test binary:

perfetto_base_unittests_targets = []
perfetto_profiling_unittests_targets = []
perfetto_tracing_unittests_targets = []
perfetto_trace_processor_unittests_targets = []
perfetto_platform_services_unittests_targets = []
perfetto_misc_unittests_targets = []

# We also have a 'perfetto_unittests_targets' list (defined at the end of this
# file), that is a concatenation of the above lists. It produces a
# "perfetto_unittests" binary, that includes all the perfetto unittests.
# That binary is running on the CI.

perfetto_base_unittests_targets += [
  "src/base:unittests",
  "src/base/threading:unittests",
  "src/protozero:unittests",
  "src/protovm:unittests",
]

perfetto_profiling_unittests_targets += [
  "src/profiling:unittests",
  "src/profiling/symbolizer:unittests",
]

perfetto_tracing_unittests_targets += [
  "src/tracing/core:unittests",
  "src/tracing/service:unittests",
  "src/tracing:unittests",
]

perfetto_misc_unittests_targets += [ "src/shared_lib/track_event:unittests" ]

if ((is_linux || is_android) && !perfetto_build_with_embedder) {
  # This test depends on pthread and can't run on non-Linux-based OS.
  perfetto_base_unittests_targets += [ "test/sanitizers:unittests" ]
}

if (enable_perfetto_tools) {
  perfetto_misc_unittests_targets += [ "src/tools:unittests" ]
}

if (enable_perfetto_ipc) {
  perfetto_base_unittests_targets += [ "src/ipc:unittests" ]
  perfetto_tracing_unittests_targets += [ "src/tracing/ipc:unittests" ]
}

if (enable_perfetto_platform_services) {
  perfetto_platform_services_unittests_targets += [
    "src/perfetto_cmd:unittests",
    "src/traced/service:unittests",
    "src/kernel_utils:unittests",
  ]
  if (enable_perfetto_traced_probes) {
    perfetto_platform_services_unittests_targets += [
      "src/traced/probes:unittests",
      "src/kallsyms:unittests",
    ]
  }
}

if (enable_perfetto_heapprofd || enable_perfetto_traced_perf) {
  perfetto_profiling_unittests_targets += [ "src/profiling/common:unittests" ]
}

if (enable_perfetto_heapprofd) {
  perfetto_profiling_unittests_targets += [
    "src/profiling/memory:unittests",
    "src/profiling/memory:ring_buffer_unittests",
  ]
}

if (enable_perfetto_traced_perf) {
  perfetto_profiling_unittests_targets +=
      [ "src/profiling/perf:producer_unittests" ]
}

if (enable_perfetto_trace_processor) {
  perfetto_trace_processor_unittests_targets += [
    "src/trace_processor:unittests",
    "src/trace_config_utils:unittests",
  ]

  if (enable_perfetto_trace_processor_sqlite) {
    perfetto_trace_processor_unittests_targets +=
        [ "src/trace_processor/metrics:unittests" ]
  }
}

if (enable_perfetto_traced_relay) {
  perfetto_platform_services_unittests_targets +=
      [ "src/traced_relay:unittests" ]
}

if (enable_perfetto_android_java_sdk) {
  perfetto_misc_unittests_targets +=
      [ "src/android_sdk/perfetto_sdk_for_jni:unittests" ]
}

perfetto_trace_processor_unittests_targets +=
    [ "src/trace_redaction:unittests" ]

# This list MUST be defined in the end of the file, to concat all the updated sub-lists.
perfetto_unittests_subset_binaries = [
  {
    name = "perfetto_base_unittests"
    targets = perfetto_base_unittests_targets
  },
  {
    name = "perfetto_profiling_unittests"
    targets = perfetto_profiling_unittests_targets
  },
  {
    name = "perfetto_tracing_unittests"
    targets = perfetto_tracing_unittests_targets
  },
  {
    name = "perfetto_trace_processor_unittests"
    targets = perfetto_trace_processor_unittests_targets
  },
  {
    name = "perfetto_platform_services_unittests"
    targets = perfetto_platform_services_unittests_targets
  },
  {
    name = "perfetto_misc_unittests"
    targets = perfetto_misc_unittests_targets
  },
]
perfetto_unittests_targets = []
foreach(subset, perfetto_unittests_subset_binaries) {
  perfetto_unittests_targets += subset.targets
}
assert(perfetto_unittests_targets != [])

template("perfetto_unittest") {
  test(target_name) {
    forward_variables_from(invoker, "*")
    deps += [
      "gn:default_deps",
      "gn:gtest_main",
    ]
    if (is_fuchsia) {
      additional_manifest_fragments =
          [ "//build/config/fuchsia/test/network.shard.test-cml" ]  # nogncheck
    }
  }
}
