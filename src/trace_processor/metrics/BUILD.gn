# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../gn/perfetto.gni")

sql_files = [ "android/android_mem.sql" ]

config("gen_config") {
  include_dirs = [ "${root_gen_dir}/${perfetto_root_path}" ]
}

action("gen_merged_sql_metrics") {
  script = "../../../tools/gen_merged_sql_metrics"
  generated_header = "${target_gen_dir}/sql_metrics.h"
  args = rebase_path(sql_files, root_build_dir) + [
           "--cpp_out",
           rebase_path(generated_header, root_build_dir),
         ]
  inputs = sql_files
  outputs = [
    generated_header,
  ]
  public_configs = [ ":gen_config" ]
}

source_set("lib") {
  sources = [
    "metrics.cc",
    "metrics.h",
  ]
  deps = [
    ":gen_merged_sql_metrics",
    "../../../gn:default_deps",
    "../../../include/perfetto/trace_processor",
    "../../../protos/perfetto/metrics:zero",
    "../../../protos/perfetto/metrics/android:zero",
    "../../protozero:protozero",
  ]
}
